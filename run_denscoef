#
echo " "
echo " =>executing: run_denscoef"
echo " "
#
#  script for executing the "denscoef.F" module
#
#     options: (must choose only one of these)
#       -Dread_my_grid    : read in your own grid definition
#       -Dgenerate_a_grid : generate a grid based on USER INPUT
#                           in grids.F
#
#     optional options:
#       -Dknudsen         : chooses the Knudsen equation
#       note: if -Dknudsen is not chosen, then the UNESCO equation
#             is used.
#

set OPTIONS = '-Ddrive_dencoef -Dgenerate_a_grid'

set MOM2 = /home/rcp/MOM_2  # base model code
set WORK = TEMP_DIR         # temp directory

set error = false
if (! -r grids.F) then
  echo '=>Error: "grids.F" is not in this UPDATES directory'
  set error = true
endif

if (! -r size.h) then
  echo '=>Error: "size.h" is not in this UPDATES directory'
  set error = true
endif

if ("$error" == "true") then
  exit
endif

echo "=>Grabbing sources from $MOM2"
mkdir $WORK
cd $WORK
set list = (stdunits.h coord.h util.F iomngr.F iomngr.h size_check.F denscoef.F)
foreach file ($list)
  cp $MOM2/$file .
end
cp ../grids.F .
cp ../size.h .

echo "=>compiling..."
f77 -P  $OPTIONS denscoef.F; f77  denscoef.i

echo "=> running...results are being written to results_denscoef"
a.out > ../results_denscoef
/bin/mv dncoef.new ../

echo " "
tail ../results_denscoef

cd ../
/bin/rm -r $WORK

